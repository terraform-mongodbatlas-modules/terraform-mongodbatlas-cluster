terraform {
  required_providers {
    mongodbatlas = {
      source  = "mongodb/mongodbatlas"
      version = "~> 2.1"
    }
    random = {
      source  = "hashicorp/random"
      version = "~> 3.0"
    }
  }
  required_version = ">= 1.9"
}

variable "project_ids" {
  type = object({
    project1 = optional(string)
    project2 = optional(string)
    project3 = optional(string)
    project4 = optional(string)
    project5 = optional(string)
  })
  default = {
    project1 = null
    project2 = null
    project3 = null
    project4 = null
    project5 = null
  }
}
variable "project_name_prefix" {
  type        = string
  default     = "ws-cluster-examples-"
  description = "Project name prefix when creating via project_generator."
}

variable "org_id" {
  type        = string
  description = "Organization ID for creating a project via project_generator."
  default     = ""
}

# Creates projects for examples that don't have a project_id in var.project_ids
module "proj" {
  for_each = toset(local.missing_project_ids)

  source       = "../project_generator"
  org_id       = var.org_id
  project_name = "${var.project_name_prefix}${each.key}"
}

locals {
  missing_project_ids = [for k, v in var.project_ids : k if v == null]
  project_ids         = { for k, v in var.project_ids : k => v != null ? v : module.proj[k].project_id }
  # tflint-ignore: terraform_unused_declarations
  project_id1 = local.project_ids.project1 # used in modules.generated.tf
  # tflint-ignore: terraform_unused_declarations
  project_id2 = local.project_ids.project2 # used in modules.generated.tf
  # tflint-ignore: terraform_unused_declarations
  project_id3 = local.project_ids.project3 # used in modules.generated.tf
  # tflint-ignore: terraform_unused_declarations
  project_id4 = local.project_ids.project4 # used in modules.generated.tf
  # tflint-ignore: terraform_unused_declarations
  project_id5 = local.project_ids.project5 # used in modules.generated.tf
}

# Example module calls are generated by tf_ws in modules.generated.tf
